
# memory struct
# 0 - size
# [1..size) - values

func set_create result
  id = mem_alloc(32)    # Set default memory size block 32 -> 31 elements
  set_clear(id)
  id

func set_size_ptr(id) result
  [id]

func set_data_ptr(id) result
  [id] + 1

func set_size(id) result
  [set_size_ptr(id)]

func set_clear(id)
  [set_size_ptr(id)] = 0 # size

func set_size_inc(id)
  size_ptr = set_size_ptr(id)
  size = [size_ptr]
  if size == mem_size(id) - 1 # substract size
    mem_x4(id)
    size_ptr = set_size_ptr(id)
  [size_ptr] = size + 1

func set_size_dec(id)
  size_ptr = set_size_ptr(id)
  [size_ptr] = [size_ptr] - 1

func set_add(id n)
  size = set_size(id)
  i = set_search(id, n)
  if i == size
    set_insert_i(id, i, n)
  else
    i_n = set_get_i(id, i)
    if n != i_n
      set_insert_i(id, i, n)

func set_insert_i(id index n)
  size = set_size(id)
  set_size_inc(id)
  offset = set_data_ptr(id) + index
  if index != size
    mem_copy(offset + 1, offset, size - index)
  [offset] = n


func set_delete_i(id index)
  size = set_size(id)
  if size != 1
    offset = set_data_ptr(id) + index
    mem_copy(offset, offset + 1, size - index - 1)
  set_size_dec(id)

func set_get_i(id index) result
  [set_data_ptr(id) + index]


func set_search(id n) result
  size = set_size(id)
  offset = 0
  if !size
    res = 0
  else
    loop $loop
      b = size >> 1 # size / 2
      a = size - b

      # left:a:(+1-0) | right:b
      half = offset + a
      half_index = half - 1

      half_n = set_get_i(id, half_index)

      if n > half_n
        if !b
          res = half
        else
          offset = offset + a
          size = b
          br $loop
      else
        if n != half_n
          if size == 1
            res = half_index
          else
            size = a
            br $loop
        else
          res = half_index
  res


func set_has(id n) result
  size = set_size(id)
  if !size
    res = 0
  else
    i = set_search(id, n)
    if i == size
      res = 0
    else
      i_n = set_get_i(id, i)
      if n == i_n
        res = 1
      else res = 0
  res


func set_delete(id n) result
  size = set_size(id)
  if !size
    res = 0
  else
    i = set_search(id, n)
    if i == size
      res = 0
    else
      i_n = set_get_i(id, i)
      if n != i_n
        res = 0
      else
        set_delete_i(id, i)
        res = 1
  res

func set_free(id)
  mem_free(id)

